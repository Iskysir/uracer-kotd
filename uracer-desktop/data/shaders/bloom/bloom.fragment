#ifdef GL_ES
#define LOWP lowp
#define MED mediump
precision mediump float;
#else
#define LOWP
#define MED
#endif
uniform MED sampler2D u_texture0;
uniform MED sampler2D u_texture1;
uniform float BloomIntensity;
uniform float BaseIntensity;
uniform float BloomSaturation;
uniform float BaseSaturation;

varying vec2 v_texCoords;

// The constants 0.3, 0.59, and 0.11 are chosen because the
// human eye is more sensitive to green light, and less to blue.
const vec3 GRAYSCALE = vec3(0.3, 0.59, 0.11);

// 0 = totally desaturated
// 1 = saturation unchanged
// higher = increase saturation
//const float BaseSat = 1;
//const float BloomSat = 1;

vec3 adjustSaturation(vec3 color, float saturation)
{
	vec3 grey = vec3(dot(color, GRAYSCALE));
	//vec3 grey = vec3((color.r+color.g+color.b)*0.333);
	return mix(grey, color, saturation);
}

void main()
{
	// lookup original and base
	vec4 original = texture2D(u_texture0, v_texCoords);
	vec4 bloom = texture2D(u_texture1, v_texCoords);

	// adjust color saturation and intensity
	original.rgb = adjustSaturation(original.rgb,BaseSaturation) * BaseIntensity;
	bloom.rgb = adjustSaturation(bloom.rgb,BloomSaturation) * BloomIntensity;

	// darken the base image in areas where ther's a lot of bloom
	// to prevent things looking excessively burned-out
	//original *= (1.0 - clamp(bloom, 0.0, 1.0));
	original *= (1.0 - bloom);

	// combine
	gl_FragColor = original + bloom;
}