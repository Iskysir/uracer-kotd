#ifdef GL_ES
#define LOWP lowp
#define MED mediump
precision mediump float;
#else
#define LOWP
#define MED
#endif
uniform MED sampler2D u_texture0;
uniform MED sampler2D u_texture1;
uniform float BloomIntensity;
uniform float OriginalIntensity;
uniform float BloomSaturation;
uniform float OriginalSaturation;

varying vec2 v_texCoords;

// The constants 0.3, 0.59, and 0.11 are chosen because the
// human eye is more sensitive to green light, and less to blue.
const vec4 GRAYSCALE = vec4(0.3, 0.59, 0.11, 0.0);

// 0 = totally desaturated
// 1 = saturation unchanged
// higher = increase saturation
//const float BaseSat = 1;
//const float BloomSat = 1;

vec4 adjustSaturation(vec4 color, float saturation)
{
	vec4 grey = vec4(dot(color, GRAYSCALE));
	return mix(grey, color, saturation);
}

void main()
{
	// lookup original and base
	vec4 original = texture2D(u_texture0, v_texCoords);
	vec4 bloom = texture2D(u_texture1, v_texCoords);

	// adjust color saturation and intensity
	original = adjustSaturation(original,OriginalSaturation) * OriginalIntensity;
	bloom = adjustSaturation(bloom,BloomSaturation) * BloomIntensity;

	// darken the base image in areas there there is a lot of bloom
	// to prevent things looking excessively burned-out
	original *= (1.0 - clamp(bloom, 0.0, 1.0));

	// combine
	gl_FragColor = original + bloom;
}